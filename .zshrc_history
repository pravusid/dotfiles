# history: https://zsh.sourceforge.io/Doc/Release/Options.html#History

setopt HIST_EXPIRE_DUPS_FIRST    # Expire duplicate entries first when trimming history.
setopt HIST_FIND_NO_DUPS         # Do not display a line previously found.
setopt HIST_IGNORE_ALL_DUPS      # Delete old recorded entry if new entry is a duplicate.
setopt HIST_IGNORE_DUPS          # Don't record an entry that was just recorded again.
setopt HIST_SAVE_NO_DUPS         # Don't write duplicate entries in the history file.

setopt HIST_IGNORE_SPACE         # Don't record an entry starting with a space.
setopt HIST_REDUCE_BLANKS        # Remove superfluous blanks before recording entry.

setopt INC_APPEND_HISTORY        # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY             # Share history between all sessions.

HISTORY_IGNORE='(cd *|ls *|touch *|cat *|bat *|less *|more *|head * | tail *)'

# https://superuser.com/a/902508
zshaddhistory() {
  [[ $1 == ${~HISTORY_IGNORE} ]] && return 2

  local j=1
  while ([[ ${${(z)1}[$j]} == *=* ]]) {
    ((j++))
  }
  whence ${${(z)1}[$j]} >| /dev/null || return 1
}

# backup_history <백업할_디렉토리> [유지할_파일_개수(기본값: 10)]
backup_history() {
  local backup_dir="$1"
  local keep_count="${2:-10}"
  local source_file="$HOME/.zsh_history"
  local today=$(date +%Y_%m_%d)
  local backup_file="${backup_dir}/history_${today}"

  if [[ ! -d "$backup_dir" ]]; then
    mkdir -p "$backup_dir" || {
      echo "백업할 디렉토리 생성 실패"
      return 1
    }
  fi

  if [[ ! -f "$source_file" ]]; then
    echo "파일을 찾을 수 없습니다: $source_file"
    return 1
  fi

  if [[ ! -f "$backup_file" ]]; then
    cp "$source_file" "$backup_file"
    if [[ $? -eq 1 ]]; then
      echo "백업 실패"
      return 1
    fi
  fi

  # 오래된 백업 파일 삭제 (최근 n개만 유지)
  local file_count=$(ls -1 "${backup_dir}"/history_* 2>/dev/null | wc -l)

  if [[ $file_count -gt $keep_count ]]; then
    ls -t "${backup_dir}"/history_* | tail -n +$((keep_count + 1)) | while read file; do
      rm "$file"
    done
  fi
}
